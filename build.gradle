apply plugin: 'groovy'

project.ext {
    VERSION="git-" + "git describe --always".execute().text
    GROOVY_VERSION="2.4.10"
}

new File('build/classes').mkdirs()
new File('build/classes/bazam.properties').text = "version=$VERSION\n"

repositories {
    mavenCentral()

    maven {
      name "gral" 
      url "http://mvn.erichseifert.de/maven2"
    }

    maven { url 'https://jitpack.io' }
}

configurations {
  compile
}

dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy', version: GROOVY_VERSION
    compile 'org.codehaus.gpars:gpars:0.12'
    compile project(':groovy-ngs-utils')
}

// Bundle all dependencies into output library
jar {

    manifest {
        attributes 'Main-Class': 'bazam.Bazam9'
    }

    from configurations.compile.collect { 
        it.isDirectory() ? it : zipTree(it) 
    }

    from("build/classes") {
        include "bazam.properties"
    }

    exclude "org/bouncycastle/**"
    exclude "META-INF/groovy*"
    exclude "META-INF/**/*groovy*"
    exclude "META-INF/BCKEY*"
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
    
    beforeTest {
        println ">>>>>>>>> Test: $it.name"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

